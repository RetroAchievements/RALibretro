/*
Copyright (C) 2018 Andre Leiradella

This file is part of RALibretro.

RALibretro is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

RALibretro is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with Foobar.  If not, see <http://www.gnu.org/licenses/>.
*/

header {
  #include "Emulator.h"
  #include <string>

  typedef const std::string const_string;
}

fsm Fsm {
  class Application as ctx;

  after {
    ctx.updateMenu();
  }

  Start {
    loadCore(Emulator core) => CoreLoaded {
      if (!ctx.loadCore(core)) {
        forbid;
      }
    }

    loadRecent(Emulator core, const_string path) => loadCore(core) => loadGame(path);

    quit() => Quit;
  }

  Quit {}

  CoreLoaded {
    loadCore(Emulator core) => unloadCore() => loadCore(core);

    unloadCore() => Start {
      ctx.unloadCore();
    }

    loadGame(const_string path) => GameRunning {
      if (!ctx.loadGame(path)) {
        forbid;
      }
    }

    loadRecent(Emulator core, const_string path) => unloadCore() => loadCore(core) => loadGame(path);

    quit() => unloadCore() => quit();
  }

  GameRunning {
    pauseGame() => GamePaused {
      if (ctx.hardcore()) {
        forbid;
      }

      ctx.pauseGame(true);
    }

    resetGame() => GameRunning {
      ctx.resetGame();
    }

    turbo() => GameTurbo {
      if (ctx.hardcore()) {
        forbid;
      }
    }

    unloadGame() => CoreLoaded {
      if (!ctx.unloadGame()) {
        forbid;
      }
    }

    loadCore(Emulator core) => unloadGame() => unloadCore() => loadCore(core);

    loadGame(const_string path) => unloadGame() => loadGame(path);

    loadRecent(Emulator core, const_string path) => unloadGame() => loadRecent(core, path);

    quit() => unloadGame() => quit();
  }

  GamePaused {
    resumeGame() => GameRunning {
      ctx.pauseGame(false);
    }

    resetGame() => resumeGame() => resetGame();

    step() => FrameStep;
    
    turbo() => GameTurbo {
      if (ctx.hardcore()) {
        forbid;
      }
    }

    unloadGame() => CoreLoaded {
      if (!ctx.unloadGame()) {
        forbid;
      }
    }

    loadCore(Emulator core) => unloadGame() => unloadCore() => loadCore(core);

    loadGame(const_string path) => unloadGame() => loadGame(path);

    loadRecent(Emulator core, const_string path) => unloadGame() => loadRecent(core, path);

    quit() => unloadGame() => quit();
  }

  FrameStep {
    resumeGame() => GamePaused;
  }

  GameTurbo {
    normal() => GameRunning;

    resetGame() => normal() => resetGame();

    unloadGame() => CoreLoaded {
      if (!ctx.unloadGame()) {
        forbid;
      }
    }

    pauseGame() => GamePaused {
      if (ctx.hardcore()) {
        forbid;
      }

      ctx.pauseGame(true);
    }

    loadCore(Emulator core) => unloadGame() => unloadCore() => loadCore(core);

    loadGame(const_string path) => unloadGame() => loadGame(path);

    loadRecent(Emulator core, const_string path) => unloadGame() => loadRecent(core, path);

    quit() => unloadGame() => quit();
  }
}
